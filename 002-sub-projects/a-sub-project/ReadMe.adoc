= a-sub-project
:toc:
:toc-placement!:

toc::[]

== Introduction

This example shows how to set up a CMake project that includes sub-projects. The
top level CMakeLists.txt calls the CMakeLists.txt in the subdirectories to
create the following:

  * a-interface - A header only library
  * a-shared-library - A shared library
  * a-static-library - A static library
  * application - An executable

The files included in this example are:

[source]
----
a-sub-project$ tree
.
├── CMakeLists.txt
├── application
│       ├── CMakeLists.txt
│       └── main.cpp
├── a-interface
│       ├── CMakeLists.txt
│       └── include
│           └── headonly
│               └── hello.hpp
├── a-shared-library
│       ├── CMakeLists.txt
│       ├── include
│       │       └── shared
│       │           └── hello.hpp
│       └── src
│           └── hello.cpp
└── a-static-library
        ├── CMakeLists.txt
        ├── include
        │       └── static
        │           └── hello.hpp
        └── src
            └── hello.cpp
----

  * link:CMakeLists.txt[CMakeLists.txt] - Top level CMakeLists.txt
  * link:application/CMakeLists.txt[application/CMakeLists.txt] - to make the executable
  * link:application/main.cpp[application/main.cpp] - source for the executable
  * link:a-interface/CMakeLists.txt[a-interface/CMakeLists.txt] - to make an interface
  * link:a-shared-library/CMakeLists.txt[a-shared-library/CMakeLists.txt] - to make a shared library
  * link:a-static-library/CMakeLists.txt[a-static-library/CMakeLists.txt] - to make a static library

[TIP]
====
In this example I have moved the header files to a sub-folder under each project's +include+
directory, while leaving the target include as the root +include+ folder. This is a good idea to prevent 
filename clashes because you have to include a file like below:
[source,cpp]
----
#include <shared/hello.hpp>
----

This also means that if you install your library for other users the default install location would be
+/usr/local/include/shared/hello.hpp+.
====

== Concepts

=== Adding a Sub-Directory

A CMakeLists.txt file can include and call sub-directories which include a CMakeLists.txt
files.

[source,cmake]
----
add_subdirectory(a-interface)
add_subdirectory(a-shared-library)
add_subdirectory(a-static-library)

add_subdirectory(application)
----

=== Referencing Sub-Project Directories

When a project is created using the `project()` command, CMake will automatically
create a number of variables which can be used to reference details about the project.
These variables can then be used by other sub-projects or the main project. For example,
to reference the source directory for a different project you can use.

[source,cmake]
----
    ${hello-interface_SOURCE_DIR}
    ${hello-shared-library_SOURCE_DIR}
    ${hello-static-library_SOURCE_DIR}
----

The variables created by CMake include:

[cols=",",options="header",]
|=======================================================================
|Variable |Info
|PROJECT_NAME | The name of the project set by the current `project()`.

|CMAKE_PROJECT_NAME |the name of the first project set by the `project()`
command, i.e. the top level project.

|PROJECT_SOURCE_DIR |The source director of the current project.

|PROJECT_BINARY_DIR |The build directory for the current project.

|name_SOURCE_DIR | The source directory of the project called "name".
In this example the source directories created would be `hello-interface_SOURCE_DIR`,
`hello-shared-library_SOURCE_DIR` and `hello-static-library_SOURCE_DIR`

|name_BINARY_DIR | The binary directory of the project called "name".
In this example the binary directories created would be `hello-interface_BINARY_DIR`,
`hello-shared-library_BINARY_DIR` and `hello-static-library_BINARY_DIR`

|=======================================================================

== Header only Libraries

If you have a library that is created as a header only library, cmake supports the +INTERFACE+
target to allow creating a target without any build output. More details can be found from
link:https://cmake.org/cmake/help/latest/command/add_library.html#interface-libraries[here]

[source,cmake]
----
add_library(${PROJECT_NAME} INTERFACE)
----

When creating the target you can also include directories for that target using
the +INTERFACE+ scope. The +INTERFACE+ scope is used to make target requirements that are used in any Libraries
that link this target but not in the compilation of the target itself.

[source,cmake]
----
target_include_directories(
    ${PROJECT_NAME}
    INTERFACE
    ${PROJECT_SOURCE_DIR}/include
)
----

=== Referencing Libraries from Sub-Projects

If a sub-project creates a library, it can be referenced by other projects by
calling the name of the target in the `target_link_libraries()` command. This
means that you don't have to reference the full path of the new library, and it
is added as a dependency.

[source,cmake]
----
target_link_libraries(
    ${PROJECT_NAME}
    PRIVATE
    hello-interface
    hello-shared-library
    hello-static-library
)
----

Alternatively, you can create an alias target which allows you to reference the
target in read only contexts.

To create an alias target run:

[source,cmake]
----
add_library(
    A_INTERFACE::HELLO
    ALIAS
    ${PROJECT_NAME}
)

add_library(
    A_SHARED_LIBRARY::HELLO
    ALIAS
    ${PROJECT_NAME}
)

add_library(
    A_STATIC_LIBRARY::HELLO
    ALIAS
    ${PROJECT_NAME}
)
----

To reference the alias, just it as follows:
[source,cmake]
----
target_link_libraries(
    ${PROJECT_NAME}
    PRIVATE
    A_INTERFACE::HELLO
    A_SHARED_LIBRARY::HELLO
    A_STATIC_LIBRARY::HELLO
)
----

=== Include directories from sub-projects

When adding the libraries from the sub-projects, starting from cmake v3, there is
no need to add the projects include directories in the included directories of the
binary using them.

This is controlled by the scope in the `target_include_directories()` command when creating
the libraries. In this example because the `application` executable links the `hello-interface`,
`hello-shared-library` and `hello-static-library` libraries it will automatically include the `${hello-interface_SOURCE_DIR}/include`,
`${hello-shared-library_SOURCE_DIR}/include` and `${hello-static-library_SOURCE_DIR}/include` folders as they are exported with the
 +PUBLIC+ and +INTERFACE+ scopes of the libraries.

== Building the example

[source,bash]
----
$ mkdir build

$ cd build/

$ cmake ..
-- The C compiler identification is ...
-- The CXX compiler identification is ...
-- Check for working C compiler: ...
-- Check for working C compiler: ... -- works
-- Detecting C compiler ABI info
-- Detecting C compiler ABI info - done
-- Check for working CXX compiler: ...
-- Check for working CXX compiler: ... -- works
-- Detecting CXX compiler ABI info
-- Detecting CXX compiler ABI info - done
-- Configuring done
-- Generating done
-- Build files have been written to: project-abusolute-path/a-sub-project/build
----
